########################################################################
# JBoss, Home of Professional Open Source
# Copyright 2009, Red Hat Middleware LLC, and individual contributors
# by the @authors tag. See the copyright.txt in the distribution for a
# full listing of individual contributors.
#
# This is free software; you can redistribute it and/or modify it
# under the terms of the GNU Lesser General Public License as
# published by the Free Software Foundation; either version 2.1 of
# the License, or (at your option) any later version.
#
# This software is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public
# License along with this software; if not, write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
# 02110-1301 USA, or see the FSF site: http://www.fsf.org.
#
# @authors Andrew Dinn
#
# ThreadMonitor
#
# A byteman script which traces thread creation, start, run and exit
#
# In order for this to run successfully the byteman jar and the byteman
# samples jar need to be added to the bootstrap classpath using the
# boot option to the -javaagent JVM command line argument. The script may
# be loaded either at JVM startup, as shown below, or submitted dynamically
# via the byteman listener (this requires use of the listener:true option)
#
# to use ths script with java program org.my.App execute the following commands
#
#  -- set the directory in which byteman has been installed
#  BYTEMAN_HOME= ...
#
#   -- we need to add byteman jar and samples jar to the boot path
#   BYTEMAN_JAR=${BYTEMAN_HOME}/lib/byteman.jar
#   SAMPLE_JAR=${BYTEMAN_HOME}/sample/lib/byteman-samples.jar
#
#   -- identify this script
#   SCRIPT={BYTEMAN_HOME}/sample/scripts/ThreadMonitor.txt
#
#   -- setting this property enables transformation of java.lang classes!
#   ALLOW_JAVA_LANG=-Dorg.jboss.byteman.quodlibet
#
#   -- compose the javaagent options and pass to the java command
#   BYTEMAN_OPTS=-javaagent:${BYTEMAN_JAR}=script:${SCRIPT},boot:${BYTEMAN_JAR},boot:${SAMPLE_JAR}
#   java ${ALLOW_JAVA_LANG} ${BYTEMAN_OPTS} org.my.App
#
#

########################################################################
#
# Rule to trace thread creation
#

RULE ThreadMonitor trace create
CLASS java.lang.Thread
METHOD <init>
HELPER org.jboss.byteman.sample.helper.ThreadMonitorHelper
AT EXIT
IF TRUE
DO traceCreate($0)
ENDRULE

########################################################################
#
# Rule to trace thread start
#

RULE ThreadMonitor trace start
CLASS java.lang.Thread
METHOD start
HELPER org.jboss.byteman.sample.helper.ThreadMonitorHelper
AT EXIT
IF TRUE
DO traceStart($0)
ENDRULE

########################################################################
#
# Rule to trace thread exit
#

RULE ThreadMonitor trace exit
CLASS java.lang.Thread
METHOD exit
HELPER org.jboss.byteman.sample.helper.ThreadMonitorHelper
AT ENTRY
IF TRUE
DO traceExit($0)
ENDRULE
